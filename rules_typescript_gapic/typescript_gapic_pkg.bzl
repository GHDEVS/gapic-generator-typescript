# Copyright 2020 Google LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

load("@com_google_api_codegen//rules_gapic:gapic_pkg.bzl", "construct_package_dir_paths")

def _typescript_gapic_src_pkg_impl(ctx):
    proto_srcs = []
    proto_inputs = []
    gapic_srcs = []

    # Note: in DIREGAPIC, protos are generated by a different target and will be located in
    # a different location, e.g. (on Mac)
    # bazel-out/darwin-fastbuild/bin/google/cloud/compute/v1/_virtual_imports/compute_small_proto/google/cloud/compute/v1/compute_small.proto
    #                                                                                             ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    # Since we only need their underlined part, we'll use .proto_source_root to get that location,
    # and pass its length N to the generated shell script to be used in "cut -b N-".
    for dep in ctx.attr.deps:
        if ProtoInfo in dep:
            protos = dep[ProtoInfo].check_deps_sources.to_list()
            root = dep[ProtoInfo].proto_source_root
            if root == '.':  # regular googleapis protos
                start_from = 1
            else:  # DIREGAPIC, we'll need to strip the proto source root
                start_from = len(root) + 2
            proto_srcs.extend([{"path": f.path, "start_from": str(start_from)} for f in protos])
            proto_inputs.extend(protos)
        else:
            gapic_srcs.extend(dep.files.to_list())

    paths = construct_package_dir_paths(ctx.attr.package_dir, ctx.outputs.pkg, ctx.label.name)

    script = """
    echo -e "{gapic_srcs}" | while read gapic_src; do
        mkdir -p "{package_dir_path}"
        unzip -q -o "$gapic_src" -d "{package_dir_path}"
    done
    echo -e "{proto_srcs}" | while read proto_src start_from; do
        dirname=`dirname "$proto_src" | cut -b $start_from-`
        mkdir -p "{package_dir_path}/protos/$dirname"
        cp -f "$proto_src" "{package_dir_path}/protos/$dirname"
    done
    chmod -R u+rw "{package_dir_path}/protos"
    rm -f "{package_dir_path}/proto.list"
    tar cfz "{pkg}" -C "{package_dir_path}/.." "{package_dir}"
    rm -rf "{package_dir_path}"
    """.format(
        gapic_srcs = "\\n".join([f.path for f in gapic_srcs]),
        proto_srcs = "\\n".join([d["path"] + " " + d["start_from"] for d in proto_srcs]),
        package_dir_path = paths.package_dir_path,
        package_dir = paths.package_dir,
        pkg = ctx.outputs.pkg.path,
    )

    ctx.actions.run_shell(
        inputs = proto_inputs + gapic_srcs,
        command = script,
        outputs = [ctx.outputs.pkg],
    )

_typescript_gapic_src_pkg = rule(
    attrs = {
        "deps": attr.label_list(allow_files = True, mandatory = True),
        "package_dir": attr.string(mandatory = True),
    },
    outputs = {"pkg": "%{name}.tar.gz"},
    implementation = _typescript_gapic_src_pkg_impl,
)

def typescript_gapic_assembly_pkg(name, deps, assembly_name = None):
    package_dir = name
    if assembly_name:
        package_dir = "%s-%s" % (assembly_name, name)
    _typescript_gapic_src_pkg(
        name = name,
        deps = deps,
        package_dir = package_dir,
    )
